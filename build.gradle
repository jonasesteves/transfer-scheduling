plugins {
    id 'idea'
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'com.jonasesteves'
version = '0.0.1-SNAPSHOT'
description = 'Technical challange for interview'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//    implementation 'commons-validator:commons-validator:1.10.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.5.5'
    implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
	runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.assertj:assertj-core:3.27.6'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
    filter {
        excludeTestsMatching("*IT")
    }
}

tasks.named('check') {
    dependsOn(test, integrationTest)
}

tasks.register('integrationTest', Test) {
    description = "Run integration tests."
    group = 'verification'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }

    filter {
        includeTestsMatching "*IT"
        excludeTestsMatching "*Test"
    }
}